include "../base/base.pav"

object Space
object Start
object Vert
object Horiz
object LowLeft
object LowRight
object TopLeft
object TopRight
object Blank

function eq(:Space,    :Space)    return true  end
function eq(:Start,    :Start)    return true  end
function eq(:Vert,     :Vert)     return true  end
function eq(:Horiz,    :Horiz)    return true  end
function eq(:LowLeft,  :LowLeft)  return true  end
function eq(:LowRight, :LowRight) return true  end
function eq(:TopLeft,  :TopLeft)  return true  end
function eq(:TopRight, :TopRight) return true  end
function eq(:Blank,    :Blank)    return true  end
function eq(x,         y)         return false end

record Cell
  type
  coord : Coord
end

record Maze
  rows
end

function get(m : Maze, c : Coord)
  return get(get(m.rows, c.row), c.col)
end

object Up
object Down
object Left
object Right

function move(:Up,    x : Coord) return Coord(x.row-1, x.col)   end
function move(:Down,  x : Coord) return Coord(x.row+1, x.col)   end
function move(:Left,  x : Coord) return Coord(x.row,   x.col-1) end
function move(:Right, x : Coord) return Coord(x.row,   x.col+1) end

function moves(:Start)    return Stack(List(Down))        end
function moves(:Vert)     return Stack(List(Up,   Down))  end
function moves(:Horiz)    return Stack(List(Left, Right)) end
function moves(:LowLeft)  return Stack(List(Up,   Right)) end
function moves(:LowRight) return Stack(List(Up,   Left))  end
function moves(:TopLeft)  return Stack(List(Down, Right)) end
function moves(:TopRight) return Stack(List(Down, Left))  end

var cells = Dict(List(Pair('.', Space),
                      Pair('S', Start),
                      Pair('|', Vert),
                      Pair('-', Horiz),
                      Pair('L', LowLeft),
                      Pair('J', LowRight),
                      Pair('F', TopLeft),
                      Pair('7', TopRight)))

var lines = split(read_file(get(_args(), 2)), "\n")

var blank = Cell(Blank, Coord(0, 0))

var start = Coord(0, 0)
var rows = List()

for row = 0, row < len(lines), row += 1 do
  var temp = List()
  var c_row = get(lines, row)
  for col = 0, col < len(c_row), col += 1 do
    var cell = get(cells, get(c_row, col))
    if cell == Start then
      start = Coord(row+1, col+1)
    end
    append(temp, Cell(cell, Coord(row+1, col+1)))
  end
  append(rows, List(blank) + temp + List(blank))
end

var blank_row = List(repeat(blank, len(get(rows, 0))))
var maze = Maze(blank_row + rows + blank_row)

var path       = Stack(List(start, start))
var path_moves = Stack(List(moves(Start)))
var directions = Stack()
var loop_set   = Set(List(start))

function make_move()
  var current_position = pop(path)
  var move_option = pop(top(path_moves))
  var next_position = move(move_option, current_position)
  if next_position == top(path) then
    move_option   = pop(top(path_moves))
    next_position = move(move_option, current_position)
  end
  push(path, current_position)
  push(path, next_position)
  push(path_moves, moves(get(maze, next_position).type))
  push(directions, move_option)
  insert(loop_set, current_position)
end

make_move()
while top(path) != start do
  make_move()
end

println(len(path)/2)

function neighbours(m : Maze, c : Cell)
  var result = List()
  for i = -1, i < 2, i += 1 do
    for j = -1, j < 2, j += 1 do
      append(result, get(m, Coord(c.coord.row+i, c.coord.col+j)))
    end
  end
  return result
end

function flood(m : Maze, c : Cell, s : Set)
  if contains(loop_set, c.coord) | contains(s, c.coord) then
    return
  end
  insert(s, c.coord)
  if c.type == Blank then
    return
  end
  foreach(neighbours(m, c), function(nb)
    flood(m, nb, s)
  end)
end

function flood(m : Maze, l : Coord, d, inside : Set)
  function look(d)
    return flood(m, get(m, move(d, l)), inside)
  end
  dispatch (get(m, l).type, d)
      (:Vert,     :Down)  look(Left)
  and (:Vert,     :Up)    look(Right)
  and (:Horiz,    :Left)  look(Up)
  and (:Horiz,    :Right) look(Down)
  and (:LowLeft,  :Down)  look(Left)  look(Down)
  and (:TopLeft,  :Right) look(Left)  look(Down)
  and (:TopRight, :Up)    look(Right) look(Up)
  and (:LowRight, :Left)  look(Right) look(Down)
  and (t,            d)
  end
end

var inside = Set()

while !is_empty(directions) do
  var c = pop(path)
  var d = pop(directions)
  flood(maze, c, d, inside)
end

println(len(inside))
